[合集 \- 学术工作效率(74\)](https://github.com)[1\.安装Amos结构方程模型分析软件的方法2023\-04\-28](https://github.com/fkxxgis/p/17361092.html)[2\.如何不显示我的电脑、回收站等图标？2023\-05\-05](https://github.com/fkxxgis/p/17374220.html)[3\.SPSS计算极值、平均值、中位数、方差、偏度、峰度、变异系数2023\-05\-08](https://github.com/fkxxgis/p/17380874.html)[4\.无需代码绘制人工神经网络ANN模型结构图的方法2023\-05\-10](https://github.com/fkxxgis/p/17388010.html)[5\.论文的技术路线流程图如何绘制？2023\-05\-19](https://github.com/fkxxgis/p/17415852.html)[6\.电脑缩放异常、显示亮度无法调整的处理办法2023\-06\-06](https://github.com/fkxxgis/p/17460286.html)[7\.EndNote参考文献格式Output Styles界面介绍2023\-06\-08](https://github.com/fkxxgis/p/17466615.html)[8\.Windows电脑环境变量（用户变量、系统变量）的修改2023\-07\-13](https://github.com/fkxxgis/p/17551129.html)[9\.如何修改电脑的BIOS密码？2023\-07\-22](https://github.com/fkxxgis/p/17573049.html)[10\.叶绿素含量测定仪SPAD\-502怎么使用？2023\-07\-29](https://github.com/fkxxgis/p/17589360.html)[11\.全球都有哪些高光谱遥感卫星？2023\-08\-19](https://github.com/fkxxgis/p/17642359.html)[12\.创建Anaconda虚拟Python环境的方法2023\-09\-09](https://github.com/fkxxgis/p/17689044.html)[13\.Anaconda虚拟环境配置Python库与Spyder编译器2023\-10\-14](https://github.com/fkxxgis/p/17764012.html)[14\.小提琴图的绘制方法：Python matplotlib实现2023\-10\-16](https://github.com/fkxxgis/p/17768423.html)[15\.如何用CAN\-EYE获取植被参数数据？2023\-10\-22](https://github.com/fkxxgis/p/17780287.html)[16\.ArcMap属性表出现乱码情况的解决2023\-10\-27](https://github.com/fkxxgis/p/17793250.html)[17\.考研保研、夏令营推免的简历模板2023\-12\-15](https://github.com/fkxxgis/p/17904071.html)[18\.安装MicroStation软件、Terrasolid插件的方法01\-22](https://github.com/fkxxgis/p/17979805)[19\.VMware虚拟机部署Linux Ubuntu系统的方法01\-24](https://github.com/fkxxgis/p/17984211)[20\.在Linux Ubuntu系统中部署C\+\+环境与Visual Studio Code软件01\-26](https://github.com/fkxxgis/p/17989213)[21\.安装Visual Studio的详细流程01\-28](https://github.com/fkxxgis/p/17992830)[22\.获取Visual Studio所用MSVC编译器版本：\_MSC\_VER数值01\-29](https://github.com/fkxxgis/p/17994267)[23\.Visual Studio部署C\+\+矩阵库Armadillo的方法01\-30](https://github.com/fkxxgis/p/17996838)[24\.如何在Visual Studio新C\+\+项目中调用之前配置过的库？01\-31](https://github.com/fkxxgis/p/17999171)[25\.在Visual Studio中部署GDAL库的C\+\+版本（包括SQLite、PROJ等依赖）02\-01](https://github.com/fkxxgis/p/18001061)[26\.C\+\+遴选出特定类型的文件或文件名符合要求的文件02\-02](https://github.com/fkxxgis/p/18002898)[27\.初步上手Git软件及GitHub平台：基本操作方法02\-05](https://github.com/fkxxgis/p/18007732)[28\.下载、安装Git并拷贝GitHub项目到本地的流程02\-06](https://github.com/fkxxgis/p/18009535)[29\.Visual Studio部署matplotlib绘图库的C\+\+版本02\-07](https://github.com/fkxxgis/p/18010863)[30\.Anaconda与Python环境在Windows中的部署03\-01](https://github.com/fkxxgis/p/18046420)[31\.Windows配置R语言、RStudio开发环境03\-03](https://github.com/fkxxgis/p/18049611)[32\.Visual Studio部署C\+\+环境下OpenCV库03\-06](https://github.com/fkxxgis/p/18055869)[33\.ArcMap的mxd文件没有数据、显示感叹号怎么办？03\-22](https://github.com/fkxxgis/p/18088768)[34\.绘制三元图、颜色空间图：R语言代码03\-27](https://github.com/fkxxgis/p/18098190)[35\.Linux电脑如何下载QGIS？03\-29](https://github.com/fkxxgis/p/18103916)[36\.Python表格处理模块xlrd在Anaconda中的安装04\-01](https://github.com/fkxxgis/p/18109161)[37\.Postman模拟浏览器网页请求并获取网页数据04\-03](https://github.com/fkxxgis/p/18112668):[樱花宇宙官网](https://yzygzn.com)[38\.多次复制Excel符合要求的数据行：Python批量实现04\-08](https://github.com/fkxxgis/p/18120657)[39\.电脑开机时报错No Bootable Device找不到索引的解决方法04\-12](https://github.com/fkxxgis/p/18131223)[40\.论文研究区域图的制作方法：ArcGIS04\-17](https://github.com/fkxxgis/p/18139839)[41\.修改中文、英文参考文献在文末列表中的顺序：EndNote04\-19](https://github.com/fkxxgis/p/18145575)[42\.ArcPy自动绘制大量地图并设置地图要素：Python04\-22](https://github.com/fkxxgis/p/18150594)[43\.Python提取文本文件（.txt）数据的方法05\-24](https://github.com/fkxxgis/p/18210950)[44\.将大量文件的拓展名中大写字母改为小写：Python实现05\-25](https://github.com/fkxxgis/p/18212290)[45\.浏览器打开JupyterLab后所有快捷键与窗口按键均失效怎么办？05\-27](https://github.com/fkxxgis/p/18215335)[46\.将编译过的C\+\+库迅速部署在Visual Studio新项目中05\-28](https://github.com/fkxxgis/p/18217914)[47\.EndNote里参考文献的期刊名显示错误怎么办？05\-29](https://github.com/fkxxgis/p/18220217)[48\.找出长时序遥感影像的缺失日期并用像素均为0的栅格填充缺失日期的文件05\-31](https://github.com/fkxxgis/p/18225102)[49\.Python依据遥感影像的分幅筛选出对应的栅格文件06\-01](https://github.com/fkxxgis/p/18225883)[50\.Python结合文件名称将多个文件复制到不同路径下06\-05](https://github.com/fkxxgis/p/18232851)[51\.mklink命令使得OneDrive同步任意一个文件夹06\-16](https://github.com/fkxxgis/p/18250917)[52\.在Ubuntu系统安装Anaconda及Python06\-18](https://github.com/fkxxgis/p/18254161)[53\.GIS数据获取：土地利用与土壤属性、DEM、水体水系数据06\-25](https://github.com/fkxxgis/p/18267371)[54\.配置h5py、netCDF4库的方法：Anaconda环境06\-26](https://github.com/fkxxgis/p/18269668)[55\.核对不同文件夹所含内容的差异并提取缺失内容：Python代码07\-03](https://github.com/fkxxgis/p/18281105)[56\.坚果云与floccus实现Chrome书签国内跨设备、跨平台同步07\-06](https://github.com/fkxxgis/p/18287538)[57\.基于FileZilla上传、下载服务器数据的方法07\-09](https://github.com/fkxxgis/p/18292769)[58\.Python按条件筛选、剔除表格数据并绘制剔除前后的直方图07\-20](https://github.com/fkxxgis/p/18312992)[59\.R语言基于表格文件的数据绘制具有多个系列的柱状图与直方图07\-22](https://github.com/fkxxgis/p/18315885)[60\.Linux服务器PBS任务队列作业提交脚本的使用方法07\-26](https://github.com/fkxxgis/p/18325140)[61\.Python按条件删除Excel表格数据的方法08\-09](https://github.com/fkxxgis/p/18350610)[62\.PowerShell快速修改多个文件的名称08\-14](https://github.com/fkxxgis/p/18359761)[63\.用whl文件安装Anaconda中的GDAL08\-17](https://github.com/fkxxgis/p/18364819)[64\.ArcGIS创建渔网工具的使用方法08\-26](https://github.com/fkxxgis/p/18381647)[65\.Python自动复制Excel数据：将各行分别重复指定次数09\-01](https://github.com/fkxxgis/p/18391303)[66\.Python批量分割Excel后逐行做差、合并文件的方法09\-03](https://github.com/fkxxgis/p/18395379)[67\.Python将表格文件中某些列的数据整体向上移动一行09\-09](https://github.com/fkxxgis/p/18404400)[68\.GIS转码的秋招历程与踩坑经历09\-22](https://github.com/fkxxgis/p/18425502)[69\.通过MobaXterm操作远程服务器电脑的方法09\-27](https://github.com/fkxxgis/p/18435640)[70\.将ASD光谱仪的.asd文件转为文本文件10\-01](https://github.com/fkxxgis/p/18443134)[71\.搜广推算法校招面试：BOSS直聘 推荐搜索系统工程师10\-08](https://github.com/fkxxgis/p/18451628)[72\.查找大量时序遥感文件缺失、不连贯的成像日期：Python代码10\-14](https://github.com/fkxxgis/p/18463655)[73\.华为校招三轮面经：通用软件开发工程师11\-03](https://github.com/fkxxgis/p/18523746)74\.合并具有文本框的Word文档：VBA代码批量操作11\-16收起
  本文介绍基于**VBA**语言，对大量含有图片、文本框与表格的**Word**文档加以批量自动合并，并在每一次合并时**添加分页符**的方法。


  在我们之前的文章中，介绍过基于**Python**语言的`python-docx`（`docx`）模块与`docxcompose`模块，对大量**Word**文档加以合并的方法；但是，基于这种方法，我们无法对**具有非明确大小的文本框**的**Word**加以合并，因为`python-docx`无法处理含有这种元素的**Word**文件。最近，一位老哥提出了合并**含有文本框**的**Word**的需求，所以就尝试用**VBA**来实现这一操作，这里就介绍一下具体的方法。


  其中，**VBA**是Visual Basic for Applications的缩写，其是基于**Visual Basic**语言的一种扩展，主要应用于微软**Office**套件中各种应用程序，例如**Word**、**Excel**与**PowerPoint**等；其允许用户创建自定义的宏和应用程序来自动执行各种任务，从而提高工作效率。目前，**VBA**主要就是应用于需要批量操作**Office**文件的各类场景中。


  本文的需求如下。现在有一个文件夹，其中包含大量文档文件，如下图所示；其中，每一个文档中，都包含图片、表格、文本框等**较为复杂的元素**。


![image](https://img2024.cnblogs.com/blog/3080295/202411/3080295-20241116155459609-693163492.png)


  我们现在希望，可以批量将文件夹中大量文档文件加以合并；并且在合并时，每次都需要在新的`1`页中合并下一个文件（也就是，不同文件的内容不要出现在`1`页中）。


  明确了需求，即可开始代码撰写。本文所需代码如下。



```
Sub merge_word()
    Dim time_start As Single: time_start = Timer
    Dim word_result As Document
    Dim word_temp As Document
    Dim file_dialog As FileDialog
    Dim str As String
    Dim file
    Dim num As Long
    
    Set word_result = ActiveDocument
    Set file_dialog = Application.FileDialog(msoFileDialogFilePicker)
    
    With file_dialog
        .AllowMultiSelect = True
        .Title = "请选择【一个或多个】需要与当前文档合并的文件"
        With .Filters
            .Clear
            .Add "Word文件", "*.doc*;*.dot*;*.wps"
            .Add "所有文件", "*.*"
        End With
        If .Show Then
            Application.ScreenUpdating = False
            num = .SelectedItems.count
            For Each file In .SelectedItems
                Set word_temp = Documents.Open(file)
                word_temp.Range.Copy
                
                word_result.Range(word_result.Range.End - 1, word_result.Range.End).Select
                
                DoEvents
                Selection.Paste
                Selection.InsertBreak
                
                word_temp.Close wdDoNotSaveChanges
            Next
            
            Application.ScreenUpdating = True
        End If
    End With
    
    Set word_result = Nothing
    Set word_temp = Nothing
    Set file_dialog = Nothing
    
    str = Format(Timer - time_start, "均已成功合并；共用时0秒！")
    str = Format(num, "您选择合并0个文件，") & str
    MsgBox str, vbInformation, "文件合并结果"
End Sub

```

  上述代码中，我们首先进行**变量声明**。`time_start`是一个`Single`类型的变量，用以记录代码开始执行的时间；`Timer`函数返回一个单精度浮点数，表示从计算机启动到现在经过的秒数。`word_result`是一个`Document`类型的变量，用以存储当前打开的**Word**文档。`word_temp`是另一个`Document`类型的变量，用以临时存储要合并的其他**Word**文档。`file_dialog`是一个`FileDialog`类型的变量，用以存储文件选择对话框对象。`str`是一个字符串类型的变量，用以存储最终要显示在消息框中的合并结果信息。`file`用以在循环中存储用户选择的每个文件路径。`num`是一个长整型变量，用以存储用户选择的文件数量。


  随后，我们**获取当前文档**。将当前正在编辑的**Word**文档赋值给`word_result`变量，这个文档就是要合并其他文档内容的结果文档。


  接下来，我们**打开文件选择对话框**。创建一个文件选择对话框对象，并逐一设置对话框的属性；其中，允许用户选择多个文件，自定义对话框标题，并设置文件类型过滤器，其中第一个表示只显示**Word**文档文件，第二个则表示显示所有类型的文件。


  紧接着，通过`If .Show Then`语句，判断用户在对话框中是否选择了文件。如果是的话，执行合并操作。其中，首先获取用户选择的文件数量；随后，循环遍历每个选择的文件——打开每个选择的文件作为**临时文档**，将**临时文档**的全部内容复制到剪贴板；将光标定位到**目标文档**（也就是结果文件）的最后一个字符处，并将剪贴板中的内容粘贴到**目标文档**的末尾，同时在粘贴的内容后插入一个分页符；最后，关闭**临时文档**而不保存更改。接下来，进行下一次遍历。其中需要注意，这里如果我们不添加`DoEvents`这句代码，会导致其下方的`Selection.Paste`这句代码报错（虽然会报错，但其实选择调试后继续按下`F5`，程序也还是可以运行）。


  最后，即可清理变量引用，并计算合并操作的耗时，将结果信息显示在消息框中。


  代码的执行方法如下。首先，在任意路径创建一个空白的**Word**文档，作为我们的结果文件。随后，在这个文档中，同时按下`Alt`键与`F11`键，进入**VBA**宏界面，如下图所示。


![](https://img2024.cnblogs.com/blog/3080295/202411/3080295-20241116155457196-2058709793.png)


  随后，在左上角的**Normal**处右键，选择“**插入**”→“**模块**”，如下图所示。


![](https://img2024.cnblogs.com/blog/3080295/202411/3080295-20241116155457265-592272245.png)


  随后，在弹出的窗口中，复制前述代码，如下图所示。


![](https://img2024.cnblogs.com/blog/3080295/202411/3080295-20241116155457333-2122896719.png)


  接下来，按下`F5`键，即可开始运行代码。其中，首先弹出一个选择文件的窗口，我们选择待合并的文件即可；如下图所示。


![](https://img2024.cnblogs.com/blog/3080295/202411/3080295-20241116155457291-1899963377.png)


  随后，点击“**确定**”，即可开始合并文件。稍等片刻，合并完成，并将弹出如下所示界面。


![](https://img2024.cnblogs.com/blog/3080295/202411/3080295-20241116155457268-838855536.png)


  此时，回到我们打开的**Word**文件中，即可看到文件已经被合并在内了。


![](https://img2024.cnblogs.com/blog/3080295/202411/3080295-20241116155457450-1780175666.png)


  其中，上图中紫色框所示区域，就是我这里待合并文件的开头部分（紫色框内红色的两段线仅仅是为了遮挡文件中的部分信息，没有别的含义，大家理解即可）——可以看到，每一次新的文件合并时，都是在新的一页操作的，符合我们的需求。


  至此，大功告成。


